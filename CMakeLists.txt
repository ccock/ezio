cmake_minimum_required(VERSION 3.11)

# Detect if being bundled via sub-directory.
if(NOT DEFINED PROJECT_NAME)
  set(EZIO_NOT_SUBPROJECT ON)
endif()

project(ezio CXX)

set_property(GLOBAL PROPERTY USE_FOLDERS ON)

if(EZIO_NOT_SUBPROJECT)
  set(ROOT_DIR ${CMAKE_SOURCE_DIR})
  set(DEPS_SOURCE_DIR ${ROOT_DIR}/build/deps)
  set(DEPS_DEPLOY_DIR ${CMAKE_BINARY_DIR})
endif()

option(EZIO_BUILD_UNITTESTS "Build ezio unittest" ON)
option(EZIO_BUILD_EXAMPLES "Build ezio examples" ON)

set(EZIO_DIR ${CMAKE_CURRENT_SOURCE_DIR})
set(EZIO_DEPS_DIR ${CMAKE_CURRENT_SOURCE_DIR}/deps)
set(EZIO_CMAKE_DIR ${EZIO_DIR}/cmake)

include(FetchContent)
set(FETCHCONTENT_BASE_DIR ${DEPS_DEPLOY_DIR})

include(${EZIO_CMAKE_DIR}/cotire.cmake)
set(EZIO_PCH_HEADER ${EZIO_DIR}/build/pch/precompile.h)

set(CMAKE_CXX_STANDARD 14)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

if(MSVC)
  include(${EZIO_CMAKE_DIR}/CompilerMSVC.cmake)

  foreach(OUTPUTCONFIG_TYPE ${CMAKE_CONFIGURATION_TYPES})
    string(TOUPPER ${OUTPUTCONFIG_TYPE} OUTPUTCONFIG)
    set(CMAKE_RUNTIME_OUTPUT_DIRECTORY_${OUTPUTCONFIG} ${CMAKE_BINARY_DIR}/${OUTPUTCONFIG_TYPE}/bin)
    set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY_${OUTPUTCONFIG} ${CMAKE_BINARY_DIR}/${OUTPUTCONFIG_TYPE}/lib)
  endforeach()
else()
  include(${EZIO_CMAKE_DIR}/CompilerUnix.cmake)

  set(CMAKE_RUNTIME_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/bin)
  set(CMAKE_ARCHIVE_OUTPUT_DIRECTORY ${CMAKE_BINARY_DIR}/lib)
endif()

add_subdirectory(ezio)

message(STATUS "EZIO_BUILD_UNITTESTS = " ${EZIO_BUILD_UNITTESTS})

if (EZIO_BUILD_UNITTESTS AND EZIO_NOT_SUBPROJECT)
  add_subdirectory(tests)
endif()

message(STATUS "EZIO_BUILD_EXAMPLES = " ${EZIO_BUILD_EXAMPLES})

if (EZIO_BUILD_EXAMPLES AND EZIO_NOT_SUBPROJECT)
  add_subdirectory(examples)
endif()
